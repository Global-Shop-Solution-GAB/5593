Program.Sub.ScreenSU.Start
Gui.frmConfig..Create
Gui.frmConfig..Size(10815,8340)
Gui.frmConfig..MinX(0)
Gui.frmConfig..MinY(0)
Gui.frmConfig..Position(0,0)
Gui.frmConfig..BackColor(-2147483633)
Gui.frmConfig..MousePointer(0)
Gui.frmConfig..Caption("Trueview Configuration")
Gui.frmConfig..Event(UnLoad,frmConfig_UnLoad)
Gui.frmConfig.gsGCWorkCentDesn.Create(GsGridControl)
Gui.frmConfig.gsGCWorkCentDesn.Size(4905,6885)
Gui.frmConfig.gsGCWorkCentDesn.Position(180,345)
Gui.frmConfig.cmdSave.Create(Button)
Gui.frmConfig.cmdSave.Size(1305,375)
Gui.frmConfig.cmdSave.Position(165,7245)
Gui.frmConfig.cmdSave.Caption("Save")
Gui.frmConfig.cmdSave.Event(Click,cmdSave_Click)
Gui.frmConfig.lbl10.Create(Label,"Design Workcentre List",True,1980,255,0,150,90,True,0,"Arial",8,-2147483633,0)
Gui.frmConfig.gsGCWorkCentShop.Create(GsGridControl)
Gui.frmConfig.gsGCWorkCentShop.Size(4905,6885)
Gui.frmConfig.gsGCWorkCentShop.Position(5535,345)
Gui.frmConfig.lbl11.Create(Label,"Shop Workcentre List",True,1980,255,0,5490,75,True,0,"Arial",8,-2147483633,0)
Gui.frmConfig.chkDept.Create(CheckBox)
Gui.frmConfig.chkDept.Size(2595,360)
Gui.frmConfig.chkDept.Position(7830,-30)
Gui.frmConfig.chkDept.Caption("Group by Department")
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
f.Intrinsic.Control.Try
	f.ODBC.Connection!con.opencompanyconnection
	v.Local.ssql.Declare
	v.Local.sWC.Declare
	v.Local.sVal.Declare
	v.Local.i.Declare(long)
	v.Local.bFileCheck.Declare(boolean)
	v.Local.sFileLocat.Declare(string)
	
	'Populate workcentres from table
	v.Local.ssql.Set("SELECT CONCAT(MACHINE,CONCAT(' - ',WC_NAME)) AS 'WC' FROM V_WORKCENTERS")
	f.Data.DataTable.CreateFromSQL("dtWC","con",v.Local.ssql)
	f.ODBC.Connection!con.Close
	F.Intrinsic.Control.For(v.Local.i,0,v.DataTable.dtWC.RowCount--,1)
		f.Intrinsic.String.Concat(v.Local.sWC,v.DataTable.dtWC(v.Local.i).WC!fieldvaltrim,"*!*",v.Local.sWC)
	F.Intrinsic.Control.Next(v.Local.i)
	
	'Check if the design file exists
	f.Intrinsic.String.Concat(v.Caller.GasDir,"\",V.Caller.CompanyCode,"_GAB_5593_CONFIG_DESN.xml",v.Local.sFileLocat)
	f.Intrinsic.File.Exists(v.Local.sFileLocat,v.Local.bFileCheck)
	f.Intrinsic.Control.If(v.Local.bFileCheck)
		f.Data.DataTable.CreateFromXML("dtD",v.Local.sFileLocat,true)
	f.Intrinsic.Control.Else
		f.Data.DataTable.Create("dtD",true)
		f.Data.DataTable.AddColumn("dtD","Workcentre","String")
	f.Intrinsic.Control.EndIf
		
	'Check if the shop file exists
	f.Intrinsic.String.Concat(v.Caller.GasDir,"\",V.Caller.CompanyCode,"_GAB_5593_CONFIG_SHOP.xml",v.Local.sFileLocat)
	f.Intrinsic.File.Exists(v.Local.sFileLocat,v.Local.bFileCheck)
	f.Intrinsic.Control.If(v.Local.bFileCheck)
		f.Data.DataTable.CreateFromXML("dtS",v.Local.sFileLocat,true)
	f.Intrinsic.Control.Else
		f.Data.DataTable.Create("dtS",true)
		f.Data.DataTable.AddColumn("dtS","Workcentre","String")
	f.Intrinsic.Control.EndIf
	
	'Check if config for department is selected
	f.Intrinsic.String.Concat(v.Caller.GasDir,"\",V.Caller.CompanyCode,"_GAB_5593_CONFIG_DEPT.txt",v.Local.sFileLocat)
	f.Intrinsic.File.Exists(v.Local.sFileLocat,v.Local.bFileCheck)
	f.Intrinsic.Control.If(v.Local.bFileCheck)
		f.Intrinsic.File.File2String(v.Local.sFileLocat,v.Local.sVal)
		gui.frmConfig.chkDept.value(v.Local.sVal)
	f.Intrinsic.Control.Else
		gui.frmConfig.chkDept.value(0)
	f.Intrinsic.Control.EndIf
	
	'Design Grid Control
	'Set Column Properties
	gui.frmConfig.gsGCWorkCentDesn.DataSource("dtD")
	gui.frmConfig.gsGCWorkCentDesn.AddGridviewFromDatatable("gvD","dtD")
	gui.frmConfig.gsGCWorkCentDesn.SetGridviewProperty("gvD","AllowAddRows","True")
	gui.frmConfig.gsGCWorkCentDesn.SetGridviewProperty("gvD","AllowDeleteRows","True")
	gui.frmConfig.gsGCWorkCentDesn.SetColumnProperty("gvD","Workcentre","ReadOnly","False")
	gui.frmConfig.gsGCWorkCentDesn.SetColumnProperty("gvD","Workcentre","AllowEdit","True")
	'Set Bold
	gui.frmConfig.gsGCWorkCentDesn.SetColumnProperty("gvD","Workcentre","HeaderFontBold","True")
	'Set drop down list
	gui.frmConfig.gsGCWorkCentDesn.ColumnEdit("gvD","Workcentre","Dropdownlist",v.Local.sWC)
	
	'Shop Grid Control
	'Set Column Properties
	gui.frmConfig.gsGCWorkCentShop.DataSource("dtS")
	gui.frmConfig.gsGCWorkCentShop.AddGridviewFromDatatable("gvS","dtS")
	gui.frmConfig.gsGCWorkCentShop.SetGridviewProperty("gvS","AllowAddRows","True")
	gui.frmConfig.gsGCWorkCentShop.SetGridviewProperty("gvS","AllowDeleteRows","True")
	gui.frmConfig.gsGCWorkCentShop.SetColumnProperty("gvS","Workcentre","ReadOnly","False")
	gui.frmConfig.gsGCWorkCentShop.SetColumnProperty("gvS","Workcentre","AllowEdit","True")
	'Set Bold
	gui.frmConfig.gsGCWorkCentShop.SetColumnProperty("gvS","Workcentre","HeaderFontBold","True")
	'Set drop down list
	gui.frmConfig.gsGCWorkCentShop.ColumnEdit("gvS","Workcentre","Dropdownlist",v.Local.sWC)
	
	'0 - None
	'1 - Top
	'2 - Bottom
	'4 - Left
	'8 - Right
	gui.frmConfig.cmdSave.ANCHOR(6)
	gui.frmConfig.gsGCWorkCentDesn.anchor(7)
	gui.frmConfig.gsGCWorkCentShop.anchor(7)
	'Show form
	gui.frmConfig..Show
	
f.Intrinsic.Control.Catch
	f.Intrinsic.UI.Msgbox(v.Ambient.ErrorDescription)
	f.Intrinsic.Control.CallSub(frmConfig_UnLoad)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.cmdSave_Click.Start
f.Intrinsic.Control.Try
	v.Local.sFileLocat.Declare(string)
	'Daily Machine Hours
	f.Intrinsic.String.Concat(v.Caller.GasDir,"\",V.Caller.CompanyCode,"_GAB_5593_CONFIG_DESN.xml",v.Local.sFileLocat)
	f.Data.DataTable.SaveToXML("dtD",0,true,v.Local.sfileLocat)
	f.Intrinsic.String.Concat(v.Caller.GasDir,"\",V.Caller.CompanyCode,"_GAB_5593_CONFIG_SHOP.xml",v.Local.sFileLocat)
	f.Data.DataTable.SaveToXML("dtS",0,true,v.Local.sfileLocat)
	f.Intrinsic.String.Concat(v.Caller.GasDir,"\",v.Caller.CompanyCode,"_GAB_5593_CONFIG_DEPT.txt",v.Local.sFileLocat)
	f.Intrinsic.File.String2File(v.Local.sFileLocat,v.Screen.frmConfig!chkDept.Value)
	f.Intrinsic.UI.Msgbox("Successfully Updated.")
f.Intrinsic.Control.Catch
	f.Intrinsic.UI.Msgbox(v.Ambient.ErrorDescription)
	f.Intrinsic.Control.CallSub(frmConfig_UnLoad)
f.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.frmConfig_UnLoad.Start
F.Intrinsic.Control.End
Program.Sub.frmConfig_UnLoad.End
